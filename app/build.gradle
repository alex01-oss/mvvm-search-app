import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'org.jetbrains.kotlin.plugin.serialization'
//    id 'com.google.gms.google-services'
//    id 'com.google.firebase.appdistribution'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

android {
    namespace = 'com.loc.searchapp'
    compileSdk = 36

    defaultConfig {
        applicationId = "com.loc.searchapp"
        minSdk = 24
        targetSdk = 36
        versionCode = 1
        versionName = "1.0"

        def youtubeApiKey = localProperties.getProperty('youtube.api.key') ?: ""
        buildConfigField("String", "YOUTUBE_API_KEY", "\"${youtubeApiKey}\"")

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    flavorDimensions = ["environment"]

    productFlavors {
        dev {
            dimension = "environment"
            applicationIdSuffix = ".dev"
            versionNameSuffix = "-dev"

            buildConfigField("String", "BASE_URL", "\"https://marketing4.taile27bf0.ts.net/api/\"")
            buildConfigField("Boolean", "ENABLE_LOGGING", "true")

            resValue("string", "app_name", "SearchApp DEV")
        }

        staging {
            dimension = "environment"
            applicationIdSuffix = ".staging"
            versionNameSuffix = "-staging"

            buildConfigField("String", "BASE_URL", "\"https://marketing4.taile27bf0.ts.net/api/\"")
            buildConfigField("Boolean", "ENABLE_LOGGING", "true")

            resValue("string", "app_name", "SearchApp STAGING")
        }

        prod {
            dimension = "environment"

            buildConfigField("String", "BASE_URL", "\"https://domain.com/api/\"")
            buildConfigField("Boolean", "ENABLE_LOGGING", "false")

            resValue("string", "app_name", "SearchApp")
        }
    }

    buildTypes {
//        debug {
//            firebaseAppDistribution {
//                groups = "testers"
//                releaseNotes = "Debug build for testing"
//            }
//        }
        release {
            minifyEnabled = false
            proguardFiles(
                    getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            )
//            firebaseAppDistribution {
//                groups = "testers"
//                releaseNotesFile = "release-notes.txt"
//            }
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    kotlin {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_21)
            freeCompilerArgs.addAll(['-XXLanguage:+PropertyParamAnnotationDefaultTargetMode'])
        }
    }
    buildFeatures {
        compose = true
        buildConfig = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.15"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    bundle {
        language {
            enableSplit = false
        }
    }
}

dependencies {
    // Core Android
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.9.4'
    implementation 'androidx.activity:activity-compose:1.11.0'

    // Compose BOM
    implementation platform('androidx.compose:compose-bom:2025.09.01')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.foundation:foundation'

    // Media3
    implementation 'androidx.media3:media3-common-ktx:1.8.0'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation platform('androidx.compose:compose-bom:2025.09.01')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    // Splash API
    implementation 'androidx.core:core-splashscreen:1.0.1'

    // Navigation
    implementation 'androidx.navigation:navigation-compose:2.9.5'

    // Dependency Injection
    implementation 'com.google.dagger:hilt-android:2.57.2'
    ksp 'com.google.dagger:hilt-android-compiler:2.57.2'
    implementation 'androidx.hilt:hilt-navigation-compose:1.3.0'

    // Network
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:1.0.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.1.0'

    // Image Loading
    implementation 'io.coil-kt:coil-compose:2.7.0'

    // Data Storage
    implementation 'androidx.datastore:datastore-preferences:1.1.7'
    implementation 'androidx.security:security-crypto-ktx:1.1.0'

    // Paging
    implementation 'androidx.paging:paging-runtime-ktx:3.3.6'
    implementation 'androidx.paging:paging-compose:3.3.6'

    // UI Components
    implementation 'androidx.compose.material:material-icons-extended:1.7.8'
    implementation 'com.mohamedrejeb.richeditor:richeditor-compose:1.0.0-rc13'
    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:13.0.0'

    // Firebase BoM
//    implementation platform('com.google.firebase:firebase-bom:34.3.0')
}